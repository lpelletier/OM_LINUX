setenv load_addr 08000000
setenv tftp_script 'tftp  $load_addr /tftpboot/u-boot/examples/tftploadscripts.img; auto $load_addr'
echo ===== U-Boot settings ===== 
setenv uboot_path /tftpboot/u-boot/u-boot.bin
setenv load_uboot 'mw.b $load_addr FF $uboot_len; tftp $load_addr $uboot_path;'
setenv update_uboot run load_uboot flash_uboot
echo ===== Linux Firmware settings =====
setenv firmware_path /tftpboot/dev/firmware.img
setenv load_firmware 'mw.b $load_addr FF $firmware_len; tftp $load_addr $firmware_path;'
setenv update_firmware run load_firmware flash_firmware
echo ===== Linux Kernel settings =====
setenv kernel_path /tftpboot/linux-2.6.12/arch/arm/boot/uImage
setenv load_kernel 'mw.b $load_addr FF $kernel_len; tftp $load_addr $kernel_path;'
setenv update_kernel run load_kernel flash_kernel
echo ===== Rootfs settings =====
setenv rootfs_path /tftpboot/buildroot/root_fs_arm_nofpu.jffs2
setenv load_rootfs 'mw.b $load_addr FF $rootfs_len; tftp $load_addr $rootfs_path;'
setenv update_rootfs run load_rootfs flash_rootfs
echo ===== allinone settings =====
setenv update_all run update_kernel update_rootfs update_uboot update_firmware
echo ===== env settings =====
setenv erase_env 'protect off $env_addr +$env_len; era $env_addr +$env_len'
echo ===== tftp ram boot settings =====
setenv netfull 'tftp 08800000 /tftpboot/fullimage.img;setenv bootargs root=/dev/ram rw rootfstype=ext2 $console $mtdparts;bootm 08800000'
setenv netroot 'tftp 08400000 /tftpboot/buildroot/root_fs_arm_nofpu.ext2.gz.img;setenv bootargs root=/dev/ram rw rootfstype=ext2 $console $mtdparts;bootm 100A0000 08400000'
echo ===== Save new definitions =====
saveenv
